#!/bin/csh -f
# **********************************************************************
# TCtracker - Tropical Storm Detection
# Copyright (C) 1997-2008, 2021 Frederic Vitart, Joe Sirutis, Ming Zhao,
# Kyle Olivo, Keren Rosado and Seth Underwood
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
# 02110-1301, USA.
# **********************************************************************

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
# FERRET MAPS OF TROPICAL STORM POSITION
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

                        set WORKDIR = /home/${user}/tmpdir
if ( ! -e $WORKDIR ) mkdir $WORKDIR
                        cd $WORKDIR
alias rm "rm -f"
alias mv "mv -f"

#=====================================================================
# --- INPUT
#=====================================================================

if ( ( $1 == "-help" ) || ( $1 == "-h" ) ) then
#--------------------------------------------------
   echo "   "
   echo "   Input Aguments:"
   echo "   1) path name of directory where tropical storm data are stored "
   echo "   2) path name for output figures                                "
   echo "   3) starting year                                               "
   echo "   4) ending year                                                 "
   echo "   5) experiment name for figures                                 "
   echo "   6) compare with obs or clim                                    "
   echo "   "
   exit
#--------------------------------------------------
else
#--------------------------------------------------
   set indir    = $1 ; shift
   set outmname   = $1 ; shift
   set year_beg = $1 ; shift                                          
   set year_end = $1 ; shift        
   set exp1     = $1 ; shift
   set exp0     = $1 ; shift
   set opt      = $1                         
#--------------------------------------------------
endif

set exp_list = ($exp0 $exp1)
set blind    = y
set domain   = 40ns   # choices: 40ns epac_atl ind_wpac sh
set land_opt = 1      # 0 = outline, 1 = solid, 2 = land-sea
set box_opt  = 0      # 0 = no, 1 = yes
set lab_opt  = 1
 
#=====================================================================
# PATH NAMES & DIRECTORIES
#=====================================================================

if ($opt == hur) then
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs/obs_hur
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_hurricanes_cat1-5
else
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_tcyclones
endif
set cyrbeg = ${year_beg}
set cyrend = ${year_end}
source $MODULESHOME/init/csh
module load grace

set atw = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/atw/util/old

# --- make output directory
if ( ! -e $outmname ) mkdir -p $outmname

#--- ferret stuff
set go_ferret  = ${atw}/run_ferret_unmapped
set fdir1      = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/my_ferret_go
set fdir2      = ${dpub2}/ferret_jnls

#=====================================================================
# --- GET DATA FOR PLOTTING
#=====================================================================

set xargs = dummy

foreach exp ( $exp_list )
#zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz
set expx = `echo  $exp | tr "_" "-"`

if ( $exp == obs ) then
  set yrb = ${cyrbeg}
  set yre = ${cyrend}
else
  set yrb = $year_beg
  set yre = $year_end
endif

if ( $exp == obs ) then
   set fori = ${dobs}/ori
else
   set fori = ${indir}/ori
endif

       @ year  = $yrb
while ( $year <= $yre)
    cat ${fori}_${year} >> ori
@ year ++
end

set tyear = ${yrb}-${yre}

mv ori fin.${expx}.${tyear}

set xargs = ( $xargs  ${expx}.${tyear} )

#zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz
end
shift xargs

#=====================================================================
# --- GET & SET JNL FILES
#=====================================================================

cp ${fdir1}/domain_${domain}.jnl   domain.jnl
cp ${fdir1}/nlots_2_40ns.jnl        nlots.jnl
cp ${fdir2}/ori.jnl                  ori.jnl

cat << EOF >> domain.jnl
define symbol land_opt = ${land_opt}
define symbol box_opt = ${box_opt}
define symbol lab_opt = ${lab_opt}
EOF

cat << EOF > fscript.jnl
\cancel mode verify
set mode metafile
go nlots.jnl
set viewport v1
go ori.jnl $xargs[1]
set viewport v2
go ori.jnl $xargs[2]
quit
EOF
#=====================================================================
# --- PLOT MAP
#=====================================================================

set mname = maps_ori

$go_ferret fscript.jnl

if ( -e metafile.plt ) then
/usr/local/ferret/bin/gksm2ps -l cps -o ${mname}.ps metafile.plt
  gzip ${mname}.ps 
    mv ${mname}.ps.gz  $outmname
endif

#=====================================================================
# --- CLEAN UP
#=====================================================================
rm *.jnl 
rm $xargs[1]
rm $xargs[2]
rm *.plt
rm fscript
#=====================================================================
exit
