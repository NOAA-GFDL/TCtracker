#!/bin/csh -f

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
#            XMGRACE graphs of trajectory duration            
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

                        set WORKDIR = /home/${user}/tmpdir
if ( ! -e $WORKDIR ) mkdir $WORKDIR
                        cd $WORKDIR
alias rm  "rm -f"
alias mv  "mv -f"

#============================================================
# INPUT
#============================================================

if ( ( $1 == "-help" ) || ( $1 == "-h" ) ) then
#--------------------------------------------------
   echo "   "
   echo "   Input Aguments:"
   echo "   1) path name of directory where tropical storm data are stored "
   echo "   2) path name for output figures                                "
   echo "   3) starting year                                               "
   echo "   4) ending year                                                 "
   echo "   5) experiment name for figures                                 "
   echo "   6) compare with obs or clim                                    "
   echo "   "
   exit
#--------------------------------------------------
else
#--------------------------------------------------
   set indir    = $1 ; shift
   set outpix   = $1 ; shift
   set year_beg = $1 ; shift                                          
   set year_end = $1 ; shift        
   set exp1     = $1 ; shift
   set exp0     = $1 ; shift
   set opt      = $1                         
#--------------------------------------------------
endif

set exp_list = ($exp0 $exp1)
set blind    = y

#============================================================
# PATH NAMES
#============================================================

if ($opt == hur) then
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs/obs_hur
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_hurricanes_cat1-5
else
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_tcyclones
endif
set cyrbeg = ${year_beg}
set cyrend = ${year_end}
source $MODULESHOME/init/csh
module load grace

# --- make output directory
if ( ! -e $outpix ) mkdir -p $outpix

# --- executable
set platform = `uname`
set prexmgr_dur = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/src_misc/prexmgr_dur.exe
cp $prexmgr_dur prexmgr_dur.exe
  
# --- parameter file for xmgrace
set xparam = ${dpub2}/xmgrace_params/ts_duration.par

#============================================================
# MISC
#============================================================

set xyears  = ${year_beg}-${year_end}

sed -e "s/YEARS/${xyears}/" $xparam > xparam_updt

#============================================================
# FOR EACH EXPERIMENT.....
#============================================================

  @ ie = 0
foreach exp ( $exp_list )
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  @ ie ++

if ( $exp == obs ) then
  set yrb = $cyrbeg
  set yre = $cyrend
else
  set yrb = $year_beg
  set yre = $year_end
endif

if ( $exp == obs ) then
   set ftraj = ${dobs}/traj
else
   set ftraj = ${indir}/traj
endif

       @ year  = $yrb
while ( $year <= $yre )
   grep start ${ftraj}_${year} | cut -c10-13 >> traj
       @ year ++
end

                    set ntpd = 4
if ( $exp == obs  ) set ntpd = 4

cat << eof > nmlst
 &input ntpd = ${ntpd}, &end
eof

prexmgr_dur.exe < nmlst >> xmgrdat

echo "&" >> xmgrdat

rm  traj

#  UPDATE PARAMETER FILE FOR EXPERIMENT NAMES ETC
sed -e "s/EXP${ie}/${exp}/" xparam_updt > xx_new
mv xx_new xparam_updt

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
end
 
#============================================================
#  PLOT GRAPHS
#============================================================

                   set xarg = ( -autoscale none )
                   set xarg = ( $xarg -printfile duration.ps   )
                   set xarg = ( $xarg -param xparam_updt )
if ( $blind == y ) set xarg = ( $xarg -hardcopy )

if ( $blind == y ) then
    gracebat $xarg  xmgrdat
else
    xmgrace $xarg  xmgrdat
endif 

if ( -e duration.ps ) then
   gzip duration.ps 
   mv   duration.ps.gz $outpix
endif 

#============================================================
rm  prexmgr_dur.exe
rm  nmlst
rm  xmgrdat
rm  xparam_updt
exit
