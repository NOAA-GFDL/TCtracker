#!/bin/csh -f
# **********************************************************************
# TCtracker - Tropical Storm Detection
# Copyright (C) 1997-2008, 2021 Frederic Vitart, Joe Sirutis, Ming Zhao,
# Kyle Olivo, Keren Rosado and Seth Underwood
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
# 02110-1301, USA.
# **********************************************************************

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
#  XMGRACE graphs of tropical storm seasonal cyle       
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

                        set WORKDIR = /home/${user}/tmpdir
if ( ! -e $WORKDIR ) mkdir $WORKDIR
                        cd $WORKDIR
alias rm  "rm -f"
alias mv  "mv -f"
#============================================================
# INPUT
#============================================================


if ( ( $1 == "-help" ) || ( $1 == "-h" ) ) then
#--------------------------------------------------
   echo "   "
   echo "   Input Aguments:"
   echo "   1) path name of directory where tropical storm data are stored "
   echo "   2) path name for output figures                                "
   echo "   3) starting year                                               "
   echo "   4) ending year                                                 "
   echo "   5) experiment name for figures                                 "
   echo "   6) compare with obs                                            "
   echo "   "
   exit
#--------------------------------------------------
else
#--------------------------------------------------
   set indir    = $1 ; shift
   set outpix   = $1 ; shift
   set year_beg = $1 ; shift                                          
   set year_end = $1 ; shift        
   set exp1     = $1 ; shift
   set exp0     = $1 ; shift
   set opt      = $1                         
#--------------------------------------------------
endif

set exp_list = ($exp0 $exp1)
set blind    = y

#============================================================
# PATH NAMES & DIRECTORIES
#============================================================

if ($opt == hur) then
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs/obs_hur
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_hurricanes_cat1-5
else
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_tcyclones
endif
set cyrbeg = ${year_beg}
set cyrend = ${year_end}
source $MODULESHOME/init/csh
module load grace

# --- make output directory
if ( ! -e $outpix ) mkdir -p $outpix

#============================================================
# MISC
#============================================================

set hemi_list = (ns nh sh)
set zzt_list  = (G WA EA WP EP NI SI AU SP SA NH SH)

#============================================================
# FOR EACH FIGURE ............
#============================================================

set xyears = ${year_beg}-${year_end}

foreach hemi ( $hemi_list )
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

set xparam = ${dpub2}/xmgrace_params/sea_cyc_${hemi}.par

sed -e "s/YEARS/${xyears}/" $xparam > xparam_updt

if ( $hemi == nh ) then
    set region_list = (NH NI WP EP WA)
else if ( $hemi == sh ) then
    set region_list = (SH SI AU SP SA)
else 
    set region_list = (NH SH)
endif

  @ ie = 0
foreach exp ( $exp_list )
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  @ ie ++

if ( $exp == obs ) then
  set yrb = $cyrbeg
  set yre = $cyrend
else
  set yrb = $year_beg
  set yre = $year_end
endif

if ( $exp == obs ) then
   set fstats = ${indir}/stats_obs_${yrb}-${yre}
else
   set fstats = ${indir}/stats_model_${yrb}-${yre}
endif

# --- BUILD INPUT FOR XMGRACE 

  @ ir = 0
foreach region ( $region_list )
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  @ ir ++

@ i = 0
foreach zzt ( $zzt_list )
@ i ++
if ( $zzt == $region ) set indx = $i
end

grep  mean $fstats > fmean
set   xmean = ` head -$indx fmean | tail -1 `
shift xmean

                                            set xshft = n
if ( $hemi == sh )                          set xshft = y
if ( ( $hemi == ns ) && ( $region == SH ) ) set xshft = y

if ( $xshft == n ) then
#------------------------------------
@ i = 1
while ( $i <= 12 )
echo $i $xmean[$i]  >> xm_${ir}
@ i ++
end
#------------------------------------
else
#------------------------------------
@ j = 1
@ i = 7
while ( $i <= 12 )
echo $j $xmean[$i] >> xm_${ir}
@ i ++
@ j ++
end
@ i = 1
while ( $i <= 6 )
echo $j $xmean[$i]  >> xm_${ir}
@ i ++
@ j ++
end
#------------------------------------
endif

echo "&" >> xm_${ir}

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
end

#--- UPDATE PARAMETER FILE FOR EXPERIMENT NAMES ETC

sed -e "s/EXP${ie}/${exp}/" xparam_updt > xx_new
mv xx_new xparam_updt

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
end

#--- PLOT GRAPHS

set pix = sea_cyc_${hemi}.ps

if ( $hemi == ns ) then
set xdat = ( -graph 0 xm_2 \
             -graph 1 xm_1 )
else
set xdat = ( -graph 0 xm_4 \
             -graph 1 xm_2 \
             -graph 2 xm_1 \
             -graph 3 xm_5 \
             -graph 4 xm_3 )
endif

#                  set xarg = ( -autoscale none )
                   set xarg = ( -autoscale y )
                   set xarg = ( $xarg -printfile $pix )
                   set xarg = ( $xarg -param xparam_updt )
if ( $blind == y ) set xarg = ( $xarg -hardcopy )

if ( $blind == y ) then
    gracebat $xarg $xdat
else
    xmgrace $xarg $xdat
endif 

if ( -e ${pix} ) then
   gzip ${pix} 
   mv   ${pix}.gz $outpix
endif 

#--- CLEAN UP
rm xm_*
rm fmean
rm xparam_updt

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
end

#============================================================
exit
