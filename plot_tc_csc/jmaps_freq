#!/bin/csh -f

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
# FERRET MAPS OF TROPICAL STORM POSITION
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

                        set WORKDIR = /home/${user}/tmpdir
if ( ! -e $WORKDIR ) mkdir $WORKDIR
                        cd $WORKDIR
alias rm "rm -f"
alias mv "mv -f"

#=====================================================================
# --- INPUT
#=====================================================================

if ( ( $1 == "-help" ) || ( $1 == "-h" ) ) then
#--------------------------------------------------
   echo "   "
   echo "   Input Aguments:"
   echo "   1) path name of directory where tropical storm data are stored "
   echo "   2) path name for output figures                                "
   echo "   3) starting year                                               "
   echo "   4) ending year                                                 "
   echo "   5) experiment name for figures                                 "
   echo "   6) compare with obs or clim                                    "
   echo "   "
   exit
#--------------------------------------------------
else
#--------------------------------------------------
   set indir    = $1 ; shift
   set outpix   = $1 ; shift
   set year_beg = $1 ; shift                                          
   set year_end = $1 ; shift        
   set exp1     = $1 ; shift
   set exp0     = $1 ; shift    
   set ptype    = $1 ; shift      
   set opt      = $1                         
#--------------------------------------------------
endif

set exp_list   = ($exp0 $exp1)
set blind      = y
set domain     = 40ns   # choices: 40ns epac_atl ind_wpac sh
set land_opt   = 1
set box_opt    = 0
set use_nl_lev = y

#=====================================================================
# PATH NAMES & DIRECTORIES
#=====================================================================

if ($opt == hur) then
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs/obs_hur
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_hurricanes_cat1-5
else
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_tcyclones
endif
set cyrbeg = ${year_beg}
set cyrend = ${year_end}
source $MODULESHOME/init/csh
module load grace

set atw = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/atw/util/old

# --- make output directory
if ( ! -e $outpix ) mkdir -p $outpix

#--- ferret stuff
set go_ferret  = ${atw}/run_ferret_unmapped
set fdir1      = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/my_ferret_go
set fdir2      = ${dpub2}/ferret_jnls

#--- executable
set freq_ori = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/src_misc/freq_ori.exe
#=====================================================================
# --- get executable, set namelist for frequency plots
#=====================================================================

cp $freq_ori freq_ori.exe
cat << eof > nml_inp
 &input do_40ns = .false., &end
eof

#=====================================================================
# --- GET DATA FOR PLOTTING
#=====================================================================

set xargs = dummy

foreach exp ( $exp_list )
#zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz
set expx = `echo  $exp | tr "_" "-"`

if ( $exp == obs ) then
  set yrb = ${cyrbeg}
  set yre = ${cyrend}
else
  set yrb = $year_beg
  set yre = $year_end
endif

if ( $exp == obs ) then
   set fori = ${dobs}/ori
else
   set fori = ${indir}/ori
endif

       @ year  = $yrb
while ( $year <= $yre)
    cat ${fori}_${year} >> ori
@ year ++
end

set tyear = ${yrb}-${yre}

freq_ori.exe < nml_inp
mv fmap fin.${expx}.${tyear}
rm -f ori

set xargs = ( $xargs  ${expx}.${tyear} )

#zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz
end
shift xargs

#=====================================================================
# --- GET & SET JNL FILES
#=====================================================================

cp ${fdir1}/domain_${domain}.jnl   domain.jnl
cp ${fdir1}/nlots_2_40ns.jnl        nlots.jnl
cp ${fdir2}/freq.jnl                 freq.jnl

if ( $use_nl_lev == y ) then
cat << eof >> domain.jnl
define symbol lev_span = (0.)(1.)(2.)(4.)(8.)(16.)(32.)
eof
else
                           set lev_span = 0.,20.,2.
if ( $domain == ind_wpac ) set lev_span = 0.,14.,2.
if ( $domain == sh       ) set lev_span = 0.,14.,2.
cat << eof >> domain.jnl
define symbol lev_span = (${lev_span})
eof
endif

cat << eof >> domain.jnl
define symbol land_opt = $land_opt
define symbol box_opt = $box_opt
eof

cat << EOF > fscript.jnl
\cancel mode verify
set mode metafile
palette exciting_cmyk
go nlots.jnl
set viewport v1
go freq.jnl $xargs[1]
set viewport v2
go freq.jnl $xargs[2]
quit
EOF

#=====================================================================
# --- PLOT MAP
#=====================================================================

$go_ferret fscript.jnl

set mname = maps_freq

if ( -e metafile.plt ) then
/usr/local/ferret/bin/gksm2ps -l cps -o ${mname}.ps metafile.plt
  gzip ${mname}.ps 
    mv ${mname}.ps.gz  $outpix
endif

#=====================================================================
# --- CLEAN UP
#=====================================================================
rm *.jnl 
rm $xargs[1]
rm $xargs[2]
rm *.plt
rm freq_ori.exe
rm nml_inp
rm fscript
#=====================================================================
exit
