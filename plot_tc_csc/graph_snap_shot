#!/bin/csh -f

                        set WORKDIR = /home/${user}/tmpdir
if ( ! -e $WORKDIR ) mkdir $WORKDIR
                        cd $WORKDIR
alias rm  "rm -f"

#============================================================
# INPUT
#============================================================

if ( ( $1 == "-help" ) || ( $1 == "-h" ) ) then
#--------------------------------------------------
   echo "   "
   echo "   Input Aguments:"
   echo "   1) path name of directory where tropical storm data are stored "
   echo "   2) path name for output figures                                "
   echo "   3) starting year                                               "
   echo "   4) ending year                                                 "
   echo "   5) experiment name for figures                                 "
   echo "   6) compare with obs                                            "
   echo "   "
   exit
#--------------------------------------------------
else
#--------------------------------------------------
   set indir    = $1 ; shift
   set outpix   = $1 ; shift
   set year_beg = $1 ; shift
   set year_end = $1 ; shift
   set exp1     = $1 ; shift
   set exp0     = $1 ; shift
   set opt      = $1
#--------------------------------------------------
endif

set exp_list = ($exp0 model)
set blind    = y

#============================================================
# DIRECTORIES, PATH NAMES, ETC
#============================================================

if ($opt == hur) then
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs/obs_hur
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_hurricanes_cat1-5
else
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_tcyclones
endif
set cyrbeg = ${year_beg}
set cyrend = ${year_end}

echo 'which module'
source $MODULESHOME/init/csh
module load grace
#the above is to get gracebat
#set gracebat = /usr/local/grace-5.1.22/bin/gracebat

# --- make output directory
if ( ! -e $outpix ) mkdir -p $outpix

# --- executable
  set freq_ori = //home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/src_misc/freq_ori.exe
  cp $freq_ori freq_ori.exe

# --- parameter file for xmgrace & topography
set geog   = ${dpub2}/xmgrace_params/geog.dat
set xparam = ${dpub2}/xmgrace_params/snap_shot.par

sed -e "s/TITLE/$exp1/" $xparam > xparam_updt

#============================================================
# MISC
#============================================================

cat << eof > nml_long
 &input
   do_40ns = .true.,
   do_map  = .false.,
   do_lon  = .true.,
   do_lat  = .false.,
   nexp    =  1,
 &end
eof

  @ yspan = ${year_end} - ${year_beg}
                     set long_rec = n
# if ( $yspan > 20 ) set long_rec = y

#============================================================
# FOR EACH EXPERIMENT.....
#============================================================

@ ie = 0
foreach exp ( $exp_list )
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
@ ie ++

#---------------------------------------------------------
# --- starting and ending years
#---------------------------------------------------------

if ( $exp == obs ) then
  set yrb = ${cyrbeg}
  set yre = ${cyrend}
else
  set yrb = $year_beg
  set yre = $year_end
endif

if ( $exp == obs ) then
   set fstats = ${indir}/stats_obs_${yrb}-${yre}
   set fori   = ${dobs}/ori
else
   set fstats = ${indir}/stats_model_${yrb}-${yre}
   set fori   = ${indir}/ori
endif

#---------------------------------------------------------
# --- means for legends
#---------------------------------------------------------

set xmean_nh = ` grep mean $fstats | head -11 | tail -1 `
set xmean_sh = ` grep mean $fstats | head -12 | tail -1 `

sed -e "s/MNH${ie}/${xmean_nh[14]}/" xparam_updt > xx_new
mv -f xx_new xparam_updt

sed -e "s/MSH${ie}/${xmean_sh[14]}/" xparam_updt > xx_new
mv -f xx_new xparam_updt

#---------------------------------------------------------
# --- time series
#---------------------------------------------------------

       @ year  = $yrb
while ( $year <= $yre )
  awk ' ( $1 == '${year}' ) { print $0 } ' $fstats >> ytmp

  set xx_nh = `head -11 ytmp | tail -1 `
  set xx_sh = `head -12 ytmp | tail -1 `
  rm -f ytmp

  if ( $long_rec == n ) then
    @ xtem_nh = $xx_nh[1] - 1900
    @ xtem_sh = $xx_sh[1] - 1900
    set xx_nh[1] = $xtem_nh
    set xx_sh[1] = $xtem_sh
  endif

  echo $xx_nh[1] $xx_nh[14] >> xts_nh
  echo $xx_sh[1] $xx_sh[14] >> xts_sh

      @ year ++
end

echo "&" >> xts_nh
echo "&" >> xts_sh

#---------------------------------------------------------
# --- seasonal cycle
#---------------------------------------------------------

grep  mean $fstats > fmean

# --- NH ---
  set xmean = ` head -11 fmean | tail -1 `
shift xmean
@ i = 1
while ( $i <= 12 )
echo $i $xmean[$i]  >> xscyc_nh
@ i ++
end

# --- SH ---
  set xmean = ` head -12 fmean | tail -1 `
shift xmean
@ j = 1
@ i = 7
while ( $i <= 12 )
echo $j $xmean[$i] >> xscyc_sh
@ i ++
@ j ++
end
@ i = 1
while ( $i <= 6 )
echo $j $xmean[$i] >> xscyc_sh
@ i ++
@ j ++
end

echo "&" >> xscyc_nh
echo "&" >> xscyc_sh
rm -f fmean

#---------------------------------------------------------
# --- distribution by longitude
#---------------------------------------------------------

       @ year  = $yrb
while ( $year <= $yre )
      cat ${fori}_${year} >> ori
@ year ++
end

freq_ori.exe < nml_long

cat flon_gl >> xlon_gl
cat flon_nh >> xlon_nh
cat flon_sh >> xlon_sh

rm -f ori
rm -f flon_*

#---------------------------------------------------------
# --- map
#---------------------------------------------------------

cat $geog > xmap_${ie}

       @ year = $yrb
while ( $year <= $yre )
awk ' { print $1 "  "$2} ' ${fori}_${year} >> xmap_${ie}
@ year ++
end

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
end

#======================================================================
#  PLOT GRAPHS
#======================================================================

                   set xarg = ( -autoscale none )
                   set xarg = ( $xarg -printfile snapshot.ps )
                   set xarg = ( $xarg -param xparam_updt)
if ( $blind == y ) set xarg = ( $xarg -hardcopy )

set xdat =       ( -graph 3 xts_nh   -graph 7 xts_sh   )
set xdat = ( $xdat -graph 2 xscyc_nh -graph 6 xscyc_sh )
set xdat = ( $xdat -graph 1 xlon_nh  -graph 5 xlon_sh  )
set xdat = ( $xdat -graph 0 xmap_1   -graph 4 xmap_2   )

if ( $blind == y ) then
    gracebat $xarg $xdat
else
    xmgrace $xarg $xdat
endif

if ( -e snapshot.ps ) then
   gzip snapshot.ps
   mv   snapshot.ps.gz $outpix
endif

#======================================================================
rm -f freq_ori.exe
rm -f nml_long
rm -f xlon_*
rm -f xscyc_*
rm -f xmap_*
rm -f xts_*
rm -f xparam_updt
exit
