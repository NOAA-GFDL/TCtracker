#!/bin/csh -f

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
#  XMGRACE graphs of tropical storm frequency by latitude       
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

                        set WORKDIR = /home/${user}/tmpdir
if ( ! -e $WORKDIR ) mkdir $WORKDIR
                        cd $WORKDIR
alias rm  "rm -f"
alias mv  "mv -f"

#============================================================
# INPUT
#============================================================

if ( ( $1 == "-help" ) || ( $1 == "-h" ) ) then
#--------------------------------------------------
   echo "   "
   echo "   Input Aguments:"
   echo "   1) path name of directory where tropical storm data are stored "
   echo "   2) path name for output figures                                "
   echo "   3) starting year                                               "
   echo "   4) ending year                                                 "
   echo "   5) experiment name for figures                                 "
   echo "   6) compare with obs                                            "
   echo "   "
   exit
#--------------------------------------------------
else
#--------------------------------------------------
   set indir    = $1 ; shift
   set outpix   = $1 ; shift
   set year_beg = $1 ; shift                                          
   set year_end = $1 ; shift        
   set exp1     = $1 ; shift
   set exp0     = $1 ; shift
   set opt      = $1                         
#--------------------------------------------------
endif

set exp_list = ($exp0 $exp1)
set blind    = y

#============================================================
# PATH NAMES & DIRECTORIES
#============================================================

if ($opt == hur) then
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs/obs_hur
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_hurricanes_cat1-5
else
    set dobs  = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/ibtracs
    set dpub2 = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/miz_run_tcyclones
endif
set cyrbeg = ${year_beg}
set cyrend = ${year_end}
source $MODULESHOME/init/csh
module load grace

# --- make output directory
if ( ! -e $outpix ) mkdir -p $outpix

# --- executable
  set freq_ori = /home/fms/local/opt/fre-analysis/test/keo/tropical_storms/miztstorm/src_misc/freq_ori.exe
  cp $freq_ori freq_ori.exe

# --- parameter file for xmgrace
set xparam = ${dpub2}/xmgrace_params/by_latitude.par

#============================================================
# MISC
#============================================================

set xyears  = ${year_beg}-${year_end}

sed -e "s/YEARS/${xyears}/" $xparam > xparam_updt

#============================================================
# FOR EACH EXPERIMENT.....
#============================================================

  @ ie = 0
foreach exp ( $exp_list )
#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  @ ie ++

if ( $exp == obs ) then
  set yrb = ${cyrbeg}
  set yre = ${cyrend}
else
  set yrb = $year_beg
  set yre = $year_end
endif

if ( $exp == obs ) then
   set fori = ${dobs}/ori
else
   set fori = ${indir}/ori
endif

       @ year  = $yrb
while ( $year <= $yre )
      cat ${fori}_${year} >> ori
@ year ++
end

cat << eof > nml_inp
 &input 
   do_40ns = .true., 
   do_map  = .false.,
   do_lon  = .false.,
   do_lat  = .true.,
 &end
eof

freq_ori.exe < nml_inp

cat flat >> xdat

rm ori
rm flat

# UPDATE PARAMETER FILE FOR EXPERIMENT NAMES ETC
sed -e "s/EXP${ie}/${exp}/" xparam_updt > xx_new
mv xx_new xparam_updt

#%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
end

#============================================================
#  PLOT GRAPHS
#============================================================

#                  set xarg =       ( -autoscale none )
                   set xarg =       ( -autoscale y )
                   set xarg = ( $xarg -printfile by_latitude.ps )
                   set xarg = ( $xarg -param xparam_updt )
if ( $blind == y ) set xarg = ( $xarg -hardcopy )

if ( $blind == y ) then
    gracebat $xarg xdat
else
    xmgrace $xarg xdat
endif 

if ( -e by_latitude.ps ) then
   gzip by_latitude.ps 
   mv   by_latitude.ps.gz $outpix
endif 

#============================================================
rm ori
rm flat
rm xdat
rm xparam_updt
rm nml_inp
rm freq_ori.exe
exit
